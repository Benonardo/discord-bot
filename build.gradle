plugins {
    id("java")
    id("application")
    id("com.github.johnrengelman.shadow") version "6.1.0"
    id("com.diffplug.spotless") version "5.8.2"
}

group "net.fabricmc"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()

    maven {
        name = "Sonatype Snapshots"
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

application {
    mainClass = "net.fabricmc.discord.bot.DiscordBot"
    applicationDefaultJvmArgs = ["--enable-preview"]
    executableDir = "run"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["--enable-preview"]
    options.release = 15
}

tasks.withType(Test).configureEach {
    jvmArgs += "--enable-preview"
}

dependencies {
    compileOnly("org.jetbrains:annotations:20.1.0")
    implementation("org.javacord:javacord:$javacordVersion")
    implementation("org.spongepowered:configurate-hocon:$configurateVersion")
    // TODO: Do we want this so we can :irritater: instead of raw id for custom emotes?
    //  https://github.com/vdurmont/emoji-java
}

shadowJar {
    mainClassName = "net.fabricmc.discord.bot.DiscordBot"
}

spotless {
    java {
        ratchetFrom "origin/master"
        licenseHeaderFile(project.file("HEADER")).yearSeparator(", ")
    }

    // Spotless tries to be smart by ignoring package-info files, however license headers are allowed there
    // TODO: Replicate on module-info if needed?
    format("package-info.java") {
        target "**/package-info.java"

        // Only update license headers when changes have occurred
        ratchetFrom("origin/master")
        // Regex is `/**` or `package`
        licenseHeaderFile(project.file("HEADER"), "/\\*\\*|package").yearSeparator(", ")
    }
}
